// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    
    Inc16(in=op,out=outInc);

    Mux16(a=op,b=outInc,sel=inc,out=outMuxInc);
    Mux16(a=outMuxInc,b=in,sel=load,out=outMuxLoad);
    Mux16(a=outMuxLoad,b[0..15]=false,sel=reset,out=outr);
    
    Register(in=outr,load=true,out=out,out=op); // that op output means the previos state that the register has before 
    //looks like an iteration procedure starts from an inital value (register previous state)
}
